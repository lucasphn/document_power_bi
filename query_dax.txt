
DEFINE
    
    VAR __tabelas = INFO.TABLES ()
    
    VAR __colunas =
        FILTER (
            INFO.COLUMNS (),
            NOT CONTAINSSTRING (
                [ExplicitName],
                "RowNumber"
            )
        )
        
    VAR __medidas = INFO.MEASURES ()
    
    VAR __formatos = INFO.FORMATSTRINGDEFINITIONS ()
    
    VAR __relacionamentos = INFO.RELATIONSHIPS ()
    
    VAR __gruposCalculos = INFO.CALCULATIONGROUPS ()
    
    VAR __itensCalculos = INFO.CALCULATIONITEMS ()
    
    VAR __tabelaFonteMedidas =
        ADDCOLUMNS (
            __medidas,
            "Tabela",
                VAR __TabelaID = [TableID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelas,
                            [ID] = __TabelaID
                        ),
                        [Name]
                    ),
            "Formato",
                VAR __MedidaID = [ID]
                RETURN
                    MAXX (
                        FILTER (
                            __formatos,
                            [ObjectID] = __MedidaID
                        ),
                        [Expression]
                    )
        )
        
    VAR __resultMedidas =
        SELECTCOLUMNS (
            __tabelaFonteMedidas,
            "Tabela", [Tabela],
            "Medida", [Name],
            "Expressao", [Expression],
            "Formato", [FormatString],
            "Está Oculto?", [IsHidden],
            "Descrição", [Description],
            "Tipo",
                SWITCH (
                    [DataType],
                    2, "String",
                    6, "Integer",
                    8, "Double",
                    9, "DateTime",
                    10, "Currency",
                    11, "Boolean"
                ),
            "Pasta", [DisplayFolder]
        )
        
    VAR __tabelaFonteColunas =
        ADDCOLUMNS (
            __colunas,
            "TabelaNome",
                VAR __TabelaID = [TableID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelas,
                            [ID] = __TabelaID
                        ),
                        [Name]
                    )
        )
        
    VAR __resultColunas =
        SELECTCOLUMNS (
            __tabelaFonteColunas,
            "Tabela", [TabelaNome],
            "Coluna",
                COALESCE (
                    [ExplicitName],
                    [InferredName]
                ),
            "Ordenada Por",
                VAR __OrderID = [SortByColumnID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelaFonteColunas,
                            [ID] = __OrderID
                        ),
                        [ExplicitName]
                    ),
            "Formato",
                COALESCE (
                    [FormatString],
                    "String"
                ),
            "Pasta", [DisplayFolder],
            "Está Oculto?", [IsHidden],
            "Expressão", [Expression]
        )
        
    VAR __tabelaFonteRelacionamentos =
        ADDCOLUMNS (
            __relacionamentos,
            "De Tabela",
                VAR __DeTabela = [FromTableID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelaFonteColunas,
                            [TableID] = __DeTabela
                        ),
                        [TabelaNome]
                    ),
            "Para Tabela",
                VAR __ParaTabela = [ToTableID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelaFonteColunas,
                            [TableID] = __ParaTabela
                        ),
                        [TabelaNome]
                    ),
            "De Coluna",
                VAR __DeColuna = [FromColumnID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelaFonteColunas,
                            [ID] = __DeColuna
                        ),
                        COALESCE (
                            [ExplicitName],
                            [InferredName]
                        )
                    ),
            "Para Coluna",
                VAR __ParaColuna = [ToColumnID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelaFonteColunas,
                            [ID] = __ParaColuna
                        ),
                        COALESCE (
                            [ExplicitName],
                            [InferredName]
                        )
                    )
        )
        
    VAR __resultRelacionamentos =
        SELECTCOLUMNS (
            __tabelaFonteRelacionamentos,
            "De Tabela", [De Tabela],
            "De Coluna", [De Coluna],
            "De Cardinalidade",
                IF (
                    [FromCardinality] = 2,
                    "*",
                    [FromCardinality]
                ),
            "De",
                "'" & [De Tabela] & "'" & "[" & [De Coluna] & "]",
            "Para Tabela", [Para Tabela],
            "Para Coluna", [Para Coluna],
            "Para Cardinalidade",
                IF (
                    [ToCardinality] = 2,
                    "*",
                    [ToCardinality]
                ),
            "Para",
                "'" & [Para Tabela] & "'" & "[" & [Para Coluna] & "]",
            "Esta Ativo?", [IsActive],
            "Sentido",
                IF (
                    [CrossFilteringBehavior] = 2,
                    "Ambos",
                    "Único"
                ),
            "De Para",
                VAR __De = "'" & [De Tabela] & "'" & "[" & [De Coluna] & "]"
                VAR __Para = "'" & [Para Tabela] & "'" & "[" & [Para Coluna] & "]"
                VAR __DeCardinalidade =
                    IF (
                        [FromCardinality] = 2,
                        "*",
                        [FromCardinality]
                    )
                VAR __ParaCardinalidade =
                    IF (
                        [ToCardinality] = 2,
                        "*",
                        [ToCardinality]
                    )
                VAR __Flecha =
                    IF (
                        [CrossFilteringBehavior] = 2,
                        "<->",
                        "<-"
                    )
                VAR __Espaco = " "
                RETURN
                    __De & __Espaco & __deCardinalidade & __Espaco & 
                    	__Flecha & __espaco & __ParaCardinalidade & __Espaco & __Para
        )
        
    VAR __tabelaFonteItensCalculos =
        ADDCOLUMNS (
            ADDCOLUMNS (
                __itensCalculos,
                "Tabela ID",
                    VAR __GrupoCalculo_ID = [CalculationGroupID]
                    RETURN
                        MAXX (
                            FILTER (
                                __gruposCalculos,
                                [ID] = __GrupoCalculo_ID
                            ),
                            [TableID]
                        ),
                "Precedência",
                    VAR __GrupoCalculo_ID = [CalculationGroupID]
                    RETURN
                        MAXX (
                            FILTER (
                                __gruposCalculos,
                                [ID] = __GrupoCalculo_ID
                            ),
                            [Precedence]
                        )
            ),
            "Grupo Cálculo",
                VAR __Tabela_ID = [Tabela ID]
                RETURN
                    MAXX (
                        FILTER (
                            __tabelas,
                            [ID] = __Tabela_ID
                        ),
                        [Name]
                    ),
            "Formato",
                VAR __Formato_ID = [FormatStringDefinitionID]
                RETURN
                    MAXX (
                        FILTER (
                            __formatos,
                            [ID] = __Formato_ID
                        ),
                        [Expression]
                    ),
            "Coluna Grupo Cálculo",
                VAR __Tabela_ID = [Tabela ID]
                VAR __Coluna_ID =
                    MINX (
                        FILTER (
                            __colunas,
                            [TableID] = __Tabela_ID
                        ),
                        [ID]
                    )
                RETURN
                    MAXX (
                        FILTER (
                            __colunas,
                            [ID] = __Coluna_ID
                        ),
                        COALESCE (
                            [ExplicitName],
                            [InferredName]
                        )
                    ),
            "Coluna Ordinal",
                VAR __Tabela_ID = [Tabela ID]
                VAR __Coluna_ID =
                    MAXX (
                        FILTER (
                            __colunas,
                            [TableID] = __Tabela_ID
                        ),
                        [ID]
                    )
                RETURN
                    MAXX (
                        FILTER (
                            __colunas,
                            [ID] = __Coluna_ID
                        ),
                        COALESCE (
                            [ExplicitName],
                            [InferredName]
                        )
                    )
        )
        
    VAR __resultGruposCalculos =
        SELECTCOLUMNS (
            __tabelaFonteItensCalculos,
            "Grupo Calculo", [Grupo Cálculo],
            "Precedência", [Precedência],
            "Item Cálculo", [Name],
            "Expressão", [Expression],
            "Formato", [Formato],
            "Ordinal", [Ordinal],
            "Coluna Grupo Cálculo", [Coluna Grupo Cálculo],
            "Coluna Ordinal", [Coluna Ordinal]
        )
        

EVALUATE
	__resultMedidas
ORDER BY
    [Tabela] ASC,
    [Medida] ASC

EVALUATE
	__resultColunas
ORDER BY
    [Tabela] ASC,
    [Coluna] ASC

EVALUATE
	__resultRelacionamentos
ORDER BY [De Para] ASC

EVALUATE
	__resultGruposCalculos
ORDER BY
    [Precedência] ASC,
    [Ordinal] ASC